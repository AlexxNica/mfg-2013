% this file contains all defenitions outside glyphs and extra parameters not listed on metaflop.com
% py#:=px#;
A_py#:=A_px#;
B_py#:=B_px#;

% some defenitions for the output and proof sheets

def endchar =
 scantokens extra_endchar;
 chardx:=w;
% makebox(proofrule);
 shipit;
 endgroup enddef;

% box and guide lines on proof sheet

% def makebox(text r) = 
% for y=0,h.o_,-d.o_, mean, cap, -des, asc, : r((0,y),(w,y)); endfor % horizontals 
% for x=0,w, :	r((x,-des),(x,asc)); endfor % verticals 

% for x=0,w, 0.1w, 0.2w, 0.3w, 0.4w, 0.5w, 0.6w, 0.7w, 0.8w, 0.9w:	r((x,-des),(x,asc)); endfor % verticals 
%%%%for x=0,w, 0.2w, 0.4w, 0.6w, 0.8w:	r((x,-d.o_),(x,h.o_)); endfor % verticals 

%if charic<>0: r((w+charic*hppp,h.o_),(w+charic*hppp,.5h.o_)); 
%fi enddef;

% initiate output mode

%mode=localfont;
mode=luxo;

mode_setup;

% pixel conversions into real points %
% metafont needs these pixel definitions to calculate the final dimensions of the parameter values

define_pixels(s,u);

define_pixels(px,py);
define_pixels(A_px,A_py,A_skeleton,B_skeleton);
define_good_y_pixels(A_asc);
define_good_y_pixels(A_des);
define_good_y_pixels(A_cap);
define_good_y_pixels(A_mean,A_box);

define_pixels(B_px,B_py);
define_good_y_pixels(B_asc);
define_good_y_pixels(B_des);
define_good_y_pixels(B_cap);
define_good_y_pixels(B_mean);
define_good_y_pixels(B_box);
define_good_y_pixels(over);
define_pixels(A_w,B_w);


vardef tanh(expr x) =
numeric ee_;
ee_ = mexp (256.0 * 2.0 * x);
((ee_-1.0)/(ee_+1.0))
enddef;

% begin character macros %
def beginfontchar(expr code, unit_width, h, d) =
beginchar(code,unit_width*u#,h,d);
pickup font_pen;
enddef;


% fixed pen settings %
%pickup pencircle xscaled px yscaled py rotated 15;
%font_pen:=savepen;

% fixed pen settings %
pickup pencircle xscaled (A_px + metapolation*(B_px-A_px)) yscaled (A_py + metapolation*(B_py-A_py)) rotated 15;
font_pen:=savepen;


% calculation for diagonal stems, like in uppercase A
vardef diag_ratio(expr a,b,y,c) =
numeric aa_, bb_, cc_; bb_=b/y; cc_=c/y; aa_=a-(b/y)*(b/y);
(a*(cc_++sqrt aa_)-bb_*cc_)/aa_ enddef;

% superness macros, can be used for quarter circle segments %

% left curves (superness) %
def super_ql(suffix i,j,m) =
 z.i{0,y.j-y.i}...((m * superness)[x.j,x.i], (m * superness)[y.i,y.j]){z.j-z.i}...z.j{x.j-x.i,0} enddef;

% right curves (superness) %
def super_qr(suffix j,k, m) =
z.j{x.k-x.j,0}...((m * superness)[x.j,x.k],(m * superness)[y.k,y.j]){z.k-z.j}...z.k{0,y.k-y.j} enddef;

% stem cut and ink trap macros

def stemcutter(expr a) =
a*(A_stemcut+metapolation*(B_stemcut-A_stemcut)) enddef;

def stemshift(expr a) =
shifted (0.5*a*(A_stemcut+metapolation*(B_stemcut-A_stemcut)),0) enddef;

def inktrapcut(expr a) =
a*(A_inktrap + metapolation * (B_inktrap-A_inktrap)) enddef;
 
def inktrap_l(expr a)=
shifted (-0.5*a*(A_inktrap + metapolation * (B_inktrap-A_inktrap)),0) enddef;

def inktrap_r(expr a)=
shifted (0.5*a*(A_inktrap + metapolation * (B_inktrap-A_inktrap)),0) enddef;

% metawidth

w:=A_w + metapolation * (B_w - A_w)

% width and sidebearing settings for each glyph

input space

% glyph input

input glyphlist

% some extra font information for TeX and LaTeX

%font_quad:=3u#+0s#;
%font_normal_space:=0s#;
%font_normal_stretch:=0u#;
%font_normal_shrink:=0u#;

% font_coding_scheme:="OT1";

